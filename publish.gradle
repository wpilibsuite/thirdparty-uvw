apply plugin: 'maven-publish'

def pubVersion = '1.8.1-1.20.3'

def baseArtifactId = 'uvw'
def artifactGroupId = 'edu.wpi.first.thirdparty'
def zipBaseName = '_GROUP_edu_wpi_first_thirdparty_ID_uvw_CLS'

def outputsFolder = file("$project.buildDir/outputs")

task cSourcesZip(type: Zip) {
    destinationDir = outputsFolder
    baseName = zipBaseName
    classifier = "sources"

    from(licenseFile) {
        into '/'
    }

    from('libuv/src') {
        into '/'
    }
}

task cHeadersZip(type: Zip) {
    destinationDir = outputsFolder
    baseName = zipBaseName
    classifier = "headers"

    from(licenseFile) {
        into '/'
    }

    from('libuv/include/uv.h') {
        into '/'
        filter { String line -> line.replaceAll('uv-', 'uv-private/uv-').replaceAll('stdint-', 'uv-private/stdint-') }
    }

    from('libuv/include') {
        into '/uv-private/'
        exclude 'uv.h'
    }

    from('uvw/src') {
        into '/'
    }
}

build.dependsOn cHeadersZip
build.dependsOn cSourcesZip

addTaskToCopyAllOutputs(cHeadersZip)
addTaskToCopyAllOutputs(cSourcesZip)

model {
    publishing {
        def UvTaskList = createComponentZipTasks($.components, 'uvw', zipBaseName, Zip, project, includeStandardZipFormat)
        def allTask
        if (!project.hasProperty('jenkinsBuild')) {
            allTask = createAllCombined(UvTaskList, 'uvw', zipBaseName, Zip, project)
        }

        publications {
            c(MavenPublication) {
                UvTaskList.each {
                    artifact it
                }
                if (!project.hasProperty('jenkinsBuild')) {
                    artifact allTask
                }
                artifact cHeadersZip
                artifact cSourcesZip

                artifactId = baseArtifactId
                groupId artifactGroupId
                version pubVersion
            }
        }
    }
}
